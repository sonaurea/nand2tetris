// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
  IN  
    x[16], y[16],  // 16-bit inputs        
    zx, // zero the x input?
    nx, // negate the x input?
    zy, // zero the y input?
    ny, // negate the y input?
    f,  // compute (out = x + y) or (out = x & y)?
    no; // negate the out output?
  OUT 
    out[16], // 16-bit output
    zr,      // if (out == 0) equals 1, else 0
    ng;      // if (out < 0)  equals 1, else 0

  PARTS:
    // ALU implementation
    // Filter X
    Mux16(a=x, sel=zx, out=zedX);
    Not16(in=zedX, out=negX);
    Mux16(a=zedX, b=negX, sel=nx, out=filteredX);
    
    // Filter Y
    Mux16(a=y, sel=zy, out=zedY);
    Not16(in=zedY, out=negY);
    Mux16(a=zedY, b=negY, sel=ny, out=filteredY);

    // Compute
    Add16(a=filteredX, b=filteredY, out=added); // Added
    And16(a=filteredX, b=filteredY, out=anded); // And
    Mux16(a=anded, b=added, sel=f, out=result);

    Not16(in=result, out=negResult);  // Invert
    Mux16(a=result, b=negResult, sel=no, out=out, out[15]=msb, out[8..15]=lowerRes, out[0..7]=upperRes);

    // Find NG MSB
    And(a=msb, b=true, out=ng);
    
    // Find ZERO
    Or8Way(in=lowerRes, out=lowerOr);
    Or8Way(in=upperRes, out=upperOr);
    Or(a=lowerOr, b=upperOr, out=orRes);
    Not(in=orRes, out=zr);
}